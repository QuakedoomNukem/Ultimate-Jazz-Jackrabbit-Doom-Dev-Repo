/*
 * Copyright (c) 2019-2023 BassSlapper89, QuakeDoomNukem, RichardDS90, Ozymandias81
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

Actor Witch 3782
{
	//$Category JJ_Goons
	Tag "Witch"
	Health 200
	Radius 12
	Height 48
	Mass 5000
	Speed 25
	Damage 4
	PainChance 256
	MONSTER
	+COUNTKILL
	+DONTHARMCLASS
	+FLOAT
	+MISSILEMORE
	+NOBLOOD
	+NOGRAVITY
	+NOINFIGHTING
	SeeSound "WITCHSEE"
	Obituary "$OB_WITCH"
	States
	{
	Spawn:
		HTWW A 3 A_Look
		Loop
	See:
		HTWW A 3 A_Chase
		Loop
	Missile:
		HTWW B 4 A_FaceTarget
		HTWW C 10 A_CustomMissile("SpellProj",40,0,0,0,0)
		Goto See
	Death:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget("Points",200)
		Stop
	Death.JazzprojIce:
		TNT1 A 0 A_GiveToTarget("Points",200)
		HTWW A 5 A_GenericFreezeDeath
		HTWW A 1 A_FreezeDeathChunks
		Wait
	Death.JazzprojFire:
	Burn:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("Points",200)
		TNT1 A 0 A_StartSound("FMTRFIRE")
		HTWW AAA 2 A_SpawnItemEx("FireDeadSpawner",0,0,0,0,0,0,0,128)
		TNT1 AAA 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-30, 30), Random(-30, 30), Random(20, 40), 0, 0, 0, 0, 128)
		TNT1 A 2 A_SpawnItemEx("FireDead",0,0,0,0,0,0,0,128)
		TNT1 A 0 A_SpawnItemEx("AshHolm",0,0,0,0,0,0,0,128)
		Stop
	}
}

Actor SpellProj
{
	Radius 6
	Height 8
	Damage 3
	Speed 25
	FastSpeed 50
	RenderStyle Add
	Projectile
	+RANDOMIZE
	DeathSound "BLSTRHIT"
	States
	{
	Spawn:
		TNT1 A 0 A_StartSound("WITCHMAG")
		HTWP A 4
		Loop
	Death:
		HTPD ABCD 3
		Stop
	}
}