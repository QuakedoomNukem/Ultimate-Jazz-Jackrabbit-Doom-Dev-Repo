Actor Bolly 3236
{
	//$Category Goons
	Tag "Bolly"
	Health 2000
	PainChance 0
	Speed 5
	Radius 27
	Height 64
	Mass 100000
	Obituary "$OB_BOLLY"
	HitObituary "$HB_BOLLY"
	MONSTER
	+COUNTKILL
	+DONTHARMSPECIES
	+FLOAT
	+FLOATBOB
	+FLOORCLIP
	+MISSILEMORE
	+NOBLOOD
	+NOGRAVITY
	+NOINFIGHTING
	+NORADIUSDMG
	SeeSound "ROBOTSEE"
	AttackSound "RFLAUNCH"
	PainSound "BOLLPAIN"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_CustomMissile("Mace0", 0, 0, 0, 2, 0)
		//BOLL A 0 A_PlaySoundEx("Hover2", "SoundSlot7", 1)
		BOLL AA 4 A_PlaySoundEx("Hover2", "SoundSlot7", 1)
		BOLL AA 4 A_Look
		Goto Spawn+2
	See:
		BOLL A 0 A_PlaySoundEx("Hover2", "SoundSlot7", 1)
		BOLL A 1 A_Chase
		Loop
	Missile:
		BOLL A 0 A_PlaySoundEx("Hover2", "SoundSlot7", 1)
		BOLL A 10 A_FaceTarget
		BOLL B 4 BRIGHT A_CustomMissile("BollyProj",16,0,359,0)
		BOLL A 4
		Goto See
	Death:
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		BOLL A 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-15, 15), Random(-15, 15), Random(20, 30), 0, 0, 0, 0, 128)
		BOLL A 2
		BOLL A 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		BOLL A 2
		BOLL A 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		BOLL A 2
		BOLL A 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		PYRO AAAAAAAAAAA 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-30, 30), Random(-30, 30), Random(20, 40), 0, 0, 0, 0, 128)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget("Points",4000)
		Stop
	}
}

Actor BollyProj
{
	Radius 8
	Height 11
	Speed 18
	Damage 3
	Projectile
	+BRIGHT
	+SEEKERMISSILE
	+THRUGHOST
	SeeSound "RFLAUNCH"
	DeathSound "SEEKXPLD"
	States
	{
	Spawn:
		INDM A 2 A_SeekerMissile(32,45, SMF_Look, 128, 10)
		INDM A 5 A_SpawnItemEx("MissileTrail", -21, 0, 0, 1)
		Goto Spawn
	Death:
		SEKE AB 3
		SEKE C 0 A_SetTranslucent (0.67,1)
		SEKE C 8 A_Explode(54,76,0)
		SEKE DEFGHIJKL 3
		Stop
	}
}