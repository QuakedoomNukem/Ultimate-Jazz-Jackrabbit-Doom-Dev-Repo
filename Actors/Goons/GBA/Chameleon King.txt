/*
 * Copyright (c) 2019-2023 BassSlapper89, QuakeDoomNukem, RichardDS90, Ozymandias81
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

Actor ChameleonKingBoss 3732
{
	//$Category JJ_Goons
	Tag "Chameleon King"
	Health 6000
	Speed 20
	Mass 10000
	Radius 24
	Height 80
	PainChance 0
	MinMissileChance 160
	DamageFactor "Queendamage", 0
	MONSTER
	+BOSS
	+BOSSDEATH
	+COUNTKILL
	+DONTHARMSPECIES
	+DONTMORPH
	+FLOORCLIP
	+MISSILEMORE
	+NOBLOOD
	+NOINFIGHTING
	+NORADIUSDMG
	+NOICEDEATH
	SeeSound "CHAMBDTH"
	PainSound "CHAMBDTH" //misstyped sound previously, or was here the intention to not produce Pain sounds? - ozy81
	DeathSound "CHAMBDTH"
	Obituary "$OB_CHABO"
	States
	{
	Spawn:
		CHKG A 10 A_Look
		Loop
	See:
		TNT1 A 0 A_StartSound("CHAMBDTH")
		CHKG AABBCCDD 3 A_Chase
		Loop
	Missile:
		CHKG E 8 A_FaceTarget
		CHKG E 20
		CHKG E 10 A_FaceTarget
		TNT1 A 0 ThrustThingZ(0,32,0,0)
		TNT1 A 0  A_QuakeEx(4,2,0,60,0,3000)
		TNT1 AAAAA 0
		CHKG F 8 A_FaceTarget
		CHKG F 4 A_CustomMissile("FallingBrickProj",28,0,0,0)
		CHKG F 4 A_CustomMissile("FallingBrickProj",28,0,90,0)
		CHKG F 4
		Goto Missile2
	Missile2:
		CHKG E 8 A_FaceTarget
		CHKG E 50
		CHKG E 10 A_FaceTarget
		TNT1 A 0 ThrustThingZ(0,32,0,0)
		TNT1 A 0  A_QuakeEx(4,2,0,60,0,3000)
		TNT1 AAAAA 0
		CHKG F 8 A_FaceTarget
		CHKG F 2 BRIGHT A_CustomMissile("FartBlast",0,0,30,0,0)
		CHKG F 1 BRIGHT A_CustomMissile("FartBlast",0,0,15,0,0)
		CHKG F 1 BRIGHT A_CustomMissile("FartBlast",0,0,0,0,0)
		CHKG F 2 BRIGHT A_CustomMissile("FartBlast",0,0,-15,0,0)
		CHKG F 2 BRIGHT A_CustomMissile("FartBlast",0,0,-30,0,0)
		CHKG F 4
		Goto Charge
	Charge:
		TNT1 A 0 A_StartSound("CHAMBDTH")
		CHKG O 4 A_SkullAttack
		CHKG P 0 A_SpawnItemEx("KingGhost1",0,0,0,0,0,0,0,128,0)
		CHKG O 4 A_SkullAttack
		CHKG P 0 A_SpawnItemEx("KingGhost1",0,0,0,0,0,0,0,128,0)
		CHKG O 4 A_SkullAttack
		CHKG P 0 A_SpawnItemEx("KingGhost1",0,0,0,0,0,0,0,128,0)
		CHKG O 4 A_SkullAttack
		CHKG P 0 A_SpawnItemEx("KingGhost1",0,0,0,0,0,0,0,128,0)
		CHKG O 4 A_SkullAttack
		CHKG P 0 A_SpawnItemEx("KingGhost1",0,0,0,0,0,0,0,128,0)
		CHKG O 4 A_SkullAttack
		CHKG P 0 A_SpawnItemEx("KingGhost1",0,0,0,0,0,0,0,128,0)
		CHKG O 4 A_SkullAttack
		CHKG P 0 A_SpawnItemEx("KingGhost1",0,0,0,0,0,0,0,128,0)
		CHKG O 4 A_SkullAttack
		CHKG P 0 A_SpawnItemEx("KingGhost1",0,0,0,0,0,0,0,128,0)
		CHKG O 0 A_Stop
		CHKG P 1
		Goto See
	Melee:
		CHKG I 10 A_FaceTarget
		CHKG J 4 A_CustomMeleeAttack(3,"CHAMELEE","","Melee",False)
		Goto See
	Death:
		TNT1 A 0 A_StartSound("CHAMBDTH")
		CHKG G 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-15, 15), Random(-15, 15), Random(20, 30), 0, 0, 0, 0, 128)
		CHKG A 2
		CHKG A 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		CHKG A 2
		CHKG A 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		CHKG A 2
		CHKG A 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		CHKG AAAAAAAAAAA 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-30, 30), Random(-30, 30), Random(20, 40), 0, 0, 0, 0, 128)
		CHKG A 3
		CHKG K 3
		CHKG L 3
		CHKG M 3
		CHKG N -1
		TNT1 A 0 A_GiveToTarget("Points",6000)
		Stop
	}
}

Actor KingGhost1
{
	Radius 40
	Height 70
	Speed 1
	Damage 0
	RenderStyle Translucent
	Alpha 0.5
	Projectile
	States
	{
	Spawn:
		CHKG O 35
		CHKG P 2 A_FadeOut(0.10)
		Goto Spawn+1
	}
}

Actor FartBlast
{
	Radius 16
	Height 8
	Speed 15
	Damage 2
	Projectile
	DamageType ChameleonKing
	RenderStyle "Translucent"
	+DONTHARMCLASS
	+FLOORCLIP
	+RIPPER
	States
	{
	Spawn:
		TNT1 A 0 A_StartSound("CHAMFART")
		CHGS ABCDE 1 BRIGHT
		CHGS DE 2 BRIGHT
		Goto Spawn+2
	Death:
		CHGS FGHIJ 5
		Stop
	}
}