/*
 * Copyright (c) 2019-2023 BassSlapper89, QuakeDoomNukem, RichardDS90, Ozymandias81
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

Actor MechBoss 31526
{
	//$Category JJ_Goons
	Tag "Turtlenator"
	Health 5000
	Radius 30
	Height 80
	Mass 100000
	Speed 15
	PainChance 0
	DamageFactor "mechboss", 0
	MinMissileChance 160
	MeleeRange 32
	MeleeDamage 44
	MONSTER
	+FLOORCLIP
	+SOLID
	+NOINFIGHTING
	+NOBLOOD
	+BOSS
	+COUNTKILL
	+DONTHARMSPECIES
	+NOICEDEATH
	SeeSound "mechsee"
	PainSound "mechpain"
	DeathSound "mechpain"
	ActiveSound "mechstep"
	Obituary "$OB_NATOR"
	States
	{
	Spawn:
		MBSS A 10 A_Look
		Loop
	See:
		MBSS AABBCCDD 3 A_Chase
		MBSS A 0 A_PlaySoundEx("mechstep", 1)
		Loop
	Missile:
		MBSS F 20 A_FaceTarget
		MBSS G 2 BRIGHT A_CustomMissile("MechBossHeatSeek",32,32,0,0,0)
		MBSS F 10 A_FaceTarget
		MBSS G 2 BRIGHT A_CustomMissile("MechBossHeatSeek",32,32,0,0,0)
		MBSS F 10 A_FaceTarget
		MBSS G 2 BRIGHT A_CustomMissile("MechBossHeatSeek",32,32,0,0,0)
		MBSS I 10 A_FaceTarget
		MBSS H 4 BRIGHT A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 BRIGHT A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 BRIGHT A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS I 10 A_FaceTarget
		MBSS h 4 BRIGHT A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 BRIGHT A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 BRIGHT A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS i 10 A_FaceTarget
		MBSS h 4 BRIGHT A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 BRIGHT A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 BRIGHT A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS A 20 A_FaceTarget
		MBSS J 20 A_PlaySound("mbfgfire")
		MBSS K 10 BRIGHT
		MBSS L 20 BRIGHT A_CustomMissile("MechBFGBall",32,0,0,0,0)
		MBSS J 20 A_FaceTarget
		Goto See
	Melee:
		TNT1 A 0 A_PlaySound
		MBSS K 10 A_FaceTarget
		MBSS L 5 A_SargAttack
		Goto See
	Death:
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		MBSS m 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-15, 15), Random(-15, 15), Random(20, 30), 0, 0, 0, 0, 128)
		MBSS m 2
		MBSS m 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		MBSS m 2
		MBSS m 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		MBSS m 2
		MBSS m 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		BMCH AAAAAAAAAAA 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-30, 30), Random(-30, 30), Random(20, 40), 0, 0, 0, 0, 128)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget("Points",6000)
		Stop
	}
}

Actor MechBossProj : ArachnotronPlasma
{
	Alpha 0.9
	SeeSound "GoonPLAS"
	DeathSound "PlasmaFI"
	DamageType Mechboss
	Height 8
	+DONTHARMCLASS
	States
	{
	Spawn:
		MPLS AB 3 BRIGHT
		Loop
	Death:
		MPLS CDEFGHI 3 BRIGHT
		Stop
	}
}

Actor MechBossHeatSeek
{
	Radius 11
	Height 8
	Speed 15
	Damage 2
	Projectile
	DamageType Mechboss
	+SEEKERMISSILE
	+RANDOMIZE
	+DEHEXPLOSION
	+ROCKETTRAIL
	+SCREENSEEKER
	+DONTHARMCLASS
	DeathSound "SEEKXPLD"
	States
	{
	Spawn:
		HRKT A 1 BRIGHT A_SeekerMissile(9,6,SMF_LOOK|SMF_CURSPEED,75,10)
		HRKT A 0 A_SpawnItemEx("MissileTrail", -21, 0, 0, 1)
		Loop
	Death:
		SEKE AB 3 BRIGHT
		SEKE C 3 BRIGHT A_Explode (4,32,0)
		SEKE DEFGHIJKL 3 BRIGHT
		Stop
	}
}

Actor MechBFGBall
{
	Radius 13
	Height 8
	Speed 25
	Damage 8
	Projectile
	+BRIGHT
	+RANDOMIZE
	+DONTHARMCLASS
	RenderStyle Add
	DamageType Mechboss
	DamageType DarkShellBoss
	Alpha 0.75
	SeeSound "mbfgfire"
	DeathSound "mbfgdie"
	States
	{
	Spawn:
		MBLL AB 2
		Loop
	Death:
		MBDD ABCD 4
		MBDD AB 4 A_SpawnItemEx("MechBFGBallExtra",0,0,0,0,0)
		MBDD BCD 4
		Stop
	}
}

Actor MechBFGBallExtra
{
	+BRIGHT
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.75
	DamageType BFGSplash
	States
	{
	Spawn:
		MBEX ABCD 8
		Stop
	}
}